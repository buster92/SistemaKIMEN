<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
	defaultButton="{fileupload}"  borderColor="#F49821" creationComplete="init()">
	
	<mx:Script>
		<![CDATA[
			import remoting.events.RemoteExceptionEvent;
			import remoting.events.RemoteResultEvent;
			import remoting.services.RemoteFileService;
			import remoting.vo.FileVO;
			import mx.controls.Alert;

			private var amfChannelId:String = "my-amfphp";
			private var amfGateway:String = "http://www.kimen.cl/amfphp/gateway.php";
			private var service:RemoteFileService;
			
			private var refUploadFile:FileReference;
			
			public var folder:String;
			public var filename:String;
			public var errorMessage:String;
			public var isUploaded:Boolean = false;
			private function init():void {
				this.addEventListener(RemoteExceptionEvent.REMOTE_EXCEPTION, handleRemoteExceptionEvent);
				this.addEventListener(RemoteResultEvent.UPLOAD_STATUS, handleRemoteResultEvent);
				// Create a new service instance
				service = new RemoteFileService( amfChannelId, amfGateway );
				this.isUploaded=false;
				
			}
			private function handleRemoteResultEvent(event:RemoteResultEvent):void {
				reset();
				// Show message from the server
				Alert.show(event.message);
				this.isUploaded=true;
			}
			private function handleRemoteExceptionEvent(event:RemoteExceptionEvent):void {
				reset();
			    Alert.show(event.message);
			    this.isUploaded=false;
            }
			public function reset():void {
				// Reset UI and filereference
				fileselected.text = "";
				this.progressBar.visible=false;
				refUploadFile = new  FileReference();
			}
            // Called to add a file for upload 
            private function browseFile():void { 
            	if(MetActivo.rutSeleccionado!="" && MetActivo.rutSeleccionado!=null){	
	               refUploadFile = new  FileReference(); 
	               var rarFilter:FileFilter = new FileFilter("Archivos .rar", "*.rar");
	               //var rarFilter:FileFilter = new FileFilter("Archivos .rar .zip .doc .docx", "*.rar;*.doc;*.docx;*.zip");
	               refUploadFile.browse([rarFilter]); 
	               refUploadFile.addEventListener(Event.SELECT,onFileSelect); 
	            }
	            else{
	            	Alert.show(errorMessage);
	            }
            } 
            // Called when a file is selected 
            private function onFileSelect(event:Event):void { 
               fileselected.text = refUploadFile.name;
               
               // Load the filereference data
               refUploadFile.load();
               fileupload.enabled = true;
            } 
            // Called when upload file is clicked
			private function uploadFile():void {
				var data:ByteArray = new ByteArray(); 
	            var file:FileVO;
	            this.progressBar.label="Subiendo archivo...";
	            this.progressBar.indeterminate=true;
	            this.progressBar.visible=true;
	            //Read the bytes into bytearray var
	            refUploadFile.data.readBytes(data, 0, refUploadFile.data.length); 
	            // Create a new FileVO instance
	            file = new FileVO();
	            file.filename = this.filename;
	            file.filedata = data;
	            service.upload(file, this.folder, this);
			}
		]]>
	</mx:Script>
	 <mx:VBox x="0" y="0" width="100%" height="100%" horizontalAlign="center">
	 	 <mx:HBox horizontalAlign="center" verticalAlign="middle">
	 	     <mx:Label text="Adjuntar archivo:"/>
	 	     <mx:TextArea id="fileselected"  width="132" height="20"/>
	 	     <mx:Button label="Seleccionar" click="browseFile();"/>
	 	     <mx:Button id="fileupload" label="Subir" click="{uploadFile()}" enabled="false"/>
	 	 </mx:HBox>
	 	 <mx:ProgressBar id="progressBar"
	 	        indeterminate="false"
	 	        visible="false"  width="190" height="33" label=""/>
	 </mx:VBox>
	    
</mx:Canvas>
